# Maintainer: Troy Engel <troyengel+arch@gmail.com>

pkgbase=petrified
pkgname=('petrified' 'petrified-systemd')
pkgver=1.0.3
pkgrel=1
changelog=petrified.archlog
arch=('any')
url="https://github.com/troyengel/petrified"
license=('GPL3')
options=('emptydirs')
source=("https://github.com/troyengel/petrified/archive/v${pkgver}.tar.gz")
md5sums=('e663dae5f6947ff4771dff783d8f1ee0')

package_petrified() {
  pkgdesc="Bash client to update dynamic DNS at freedns.afraid.org"
  depends=('util-linux' 'coreutils' 'bash' 'iproute2' 'curl')
  backup=('etc/petrified.conf')
  install=petrified.archinst

  cd "${srcdir}/${pkgbase}-${pkgver}"

  # Install the system bits
  install -Dm0755 petrified "${pkgdir}/usr/bin/petrified"
  install -Dm0644 petrified.1 "${pkgdir}/usr/share/man/man1/petrified.1"
  install -Dm0600 petrified.conf "${pkgdir}/etc/petrified.conf" 
  install -dm0755 "${pkgdir}/var/cache/petrified"

  # Install examples for the user
  install -dm0755 "${pkgdir}/usr/share/doc/${pkgname}/extra"
  install -m0644 README.pod "${pkgdir}/usr/share/doc/${pkgname}"
  install -m0644 extra/* "${pkgdir}/usr/share/doc/${pkgname}/extra"
}

package_petrified-systemd() {
  pkgdesc="Unit files for petrified systemd usage"
  depends=('petrified' 'systemd')

  cd "${srcdir}/${pkgbase}-${pkgver}"

  # Systemd files
  install -dm0755 "${pkgdir}/usr/lib/systemd/system"
  install -m0644 systemd/petrified.target systemd/petrified.timer \
                 systemd/petrified@.service \
                 "${pkgdir}/usr/lib/systemd/system"
}
